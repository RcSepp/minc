/* ---------------------------------------------------------------------------*\
|                                                                              |
|                               LLVM-C FUNCTIONS                               |
|                                                                              |
\* ---------------------------------------------------------------------------*/


import <llvm_api.acc>;


/* ---------------------------------------------------------------------------*\
Name: Simplified variable assignment
Template: $I = $E
Depends on: return $E
Notes: This expression does not check for errors and it does not return $E
\* ---------------------------------------------------------------------------*/
exprdef<void> $I = $E {
	LLVMEXBuildStore($1.codegen(parentBlock), LookupScope(parentBlock, $0));
	return LLVMConstNull(LLVMVoidType());
}

paws_exprdef<void> addToScope($E<BlockExprAST>, $E<IdExprAST>, $E<BaseType>, $E<LLVMValueRef>) {
	import paws.array;
	args = [
		$0.codegen(parentBlock),
		$1.codegen(parentBlock),
		$2.codegen(parentBlock),
		$3.codegen(parentBlock)
	];
	LLVMBuildCall(AddToScope, args, 4, "");
	return LLVMConstNull(LLVMVoidType());
}


/* ---------------------------------------------------------------------------*\
|                                                                              |
|                              PAWS TO LLVM CASTS                              |
|                                                                              |
\* ---------------------------------------------------------------------------*/


paws_castdef<int> PawsInt {
	return LLVMConstInt(LLVMInt32Type(), $0.codegen(parentBlock), 1);
}

paws_castdef<half> PawsDouble {
	return LLVMConstReal(LLVMHalfType(), $0.codegen(parentBlock));
}
paws_castdef<float> PawsDouble {
	return LLVMConstReal(LLVMFloatType(), $0.codegen(parentBlock));
}
paws_castdef<double> PawsDouble {
	return LLVMConstReal(LLVMDoubleType(), $0.codegen(parentBlock));
}

paws_castdef<string> PawsString {
	str = $0.codegen(parentBlock);
	glob = LLVMAddGlobal(LLVMArrayType(LLVMInt8Type(), str.length + 1), "MY_CONSTANT");
	LLVMSetLinkage(glob, 9);
	LLVMSetGlobalConstant(glob, 1);
	LLVMSetInitializer(glob, LLVMConstString(str, str.length + 1, 1));
	LLVMSetUnnamedAddress(glob, 2);
	LLVMSetAlignment(glob, 1);

	return LLVMConstInBoundsGEP2(
		glob,
		LLVMConstInt(LLVMInt64Type(), 0, 0),
		LLVMConstInt(LLVMInt64Type(), 0, 0)
	);
}


/* ---------------------------------------------------------------------------*\
|                                                                              |
|                                  LLVM TYPES                                  |
|                                                                              |
\* ---------------------------------------------------------------------------*/


/* ---------------------------------------------------------------------------*\
Name: LLVMValueRef declaration
Template: LLVMValueRef $I
Depends on: none
\* ---------------------------------------------------------------------------*/
stmtdef LLVMValueRef $I {
	addToScope(parentBlock, $0, LLVMValueRef, LLVMEXBuildAlloca(LLVMPointerType(LLVMEXGetTypeByName("struct.LLVMOpaqueValue"), 0), ""));
}

/* ---------------------------------------------------------------------------*\
Name: LLVMValueRef declaration with initialization
Template: LLVMValueRef $I = $E<LLVMValueRef>
Depends on: LLVMValueRef $I
\* ---------------------------------------------------------------------------*/
stmtdef LLVMValueRef $I = $E<LLVMValueRef> {
	LLVMValueRef foo;
	foo = LLVMEXBuildAlloca(LLVMPointerType(LLVMEXGetTypeByName("struct.LLVMOpaqueValue"), 0), "");
	LLVMEXBuildStore($1.codegen(parentBlock), foo);
	addToScope(parentBlock, $0, LLVMValueRef, foo);
}

stmtdef LLVMValueRef $I[$E<int>] {
	// Fixed size array:
	/*addToScope(
		parentBlock,
		$0,
		LLVMValueRef,
		LLVMEXBuildAlloca(
			LLVMArrayType(LLVMPointerType(LLVMEXGetTypeByName("struct.LLVMOpaqueValue"), 0), atoi($1.value)),
			""
		)
	);*/

	// Dynamic array:
	LLVMValueRef array = LLVMEXBuildArrayAlloca(
		LLVMPointerType(LLVMEXGetTypeByName("struct.LLVMOpaqueValue"), 0),
		$1.codegen(parentBlock),
		""
	);
	LLVMValueRef arrayptr = LLVMEXBuildAlloca(LLVMPointerType(LLVMPointerType(LLVMEXGetTypeByName("struct.LLVMOpaqueValue"), 0), 0), "");
	LLVMEXBuildStore(
		array,
		arrayptr
	);
	addToScope(
		parentBlock,
		$0,
		LLVMValueRefPtr,
		arrayptr
	);
}

//TODO: Generalize to `exprdef<gettype($2, parentBlock)> $E[$E<int>] = $E {`
exprdef<LLVMValueRef> $E<LLVMValueRefPtr>[$E<int>] = $E<LLVMValueRef> {
	LLVMValueRef var = $0.codegen(parentBlock);
	LLVMValueRef idx = $1.codegen(parentBlock);

	LLVMValueRef gep = LLVMEXBuildInBoundsGEP1(var, idx, "");

	LLVMValueRef expr = $2.codegen(parentBlock);
	LLVMEXBuildStore(
		expr,
		gep
	);
	return expr;
}

//TODO: Untested
exprdef<LLVMValueRef> $E<LLVMValueRefPtr>[$E<int>] {
	LLVMValueRef var = $0.codegen(parentBlock);
	LLVMValueRef idx = $1.codegen(parentBlock);

	LLVMValueRef gep = LLVMEXBuildInBoundsGEP1(var, idx, "");
	LLVMValueRef val = LLVMEXBuildLoad(gep, "");
	LLVMSetAlignment(val, 8);
	return val;
}

/* ---------------------------------------------------------------------------*\
Cast: LLVMValueRef to bool
Depends on: return $E
\* ---------------------------------------------------------------------------*/
castdef<bool> $E<LLVMValueRef> { return LLVMEXBuildIsNotNull($0.codegen(parentBlock), ""); }

/* ---------------------------------------------------------------------------*\
Name: LLVMBasicBlockRef declaration
Template: LLVMBasicBlockRef $I
Depends on: none
\* ---------------------------------------------------------------------------*/
stmtdef LLVMBasicBlockRef $I {
	addToScope(parentBlock, $0, LLVMBasicBlockRef, LLVMEXBuildAlloca(LLVMPointerType(LLVMEXGetTypeByName("struct.LLVMOpaqueBasicBlock"), 0), ""));
}

/* ---------------------------------------------------------------------------*\
Name: LLVMBasicBlockRef declaration with initialization
Template: LLVMBasicBlockRef $I = $E<LLVMBasicBlockRef>
Depends on: LLVMBasicBlockRef $I
\* ---------------------------------------------------------------------------*/
stmtdef LLVMBasicBlockRef $I = $E<LLVMBasicBlockRef> {
	LLVMValueRef foo = LLVMEXBuildAlloca(LLVMPointerType(LLVMEXGetTypeByName("struct.LLVMOpaqueBasicBlock"), 0), "");
	LLVMEXBuildStore($1.codegen(parentBlock), foo);
	addToScope(parentBlock, $0, LLVMBasicBlockRef, foo);
}

/* ---------------------------------------------------------------------------*\
Name: LLVMTypeRef declaration
Template: LLVMTypeRef $I
Depends on: none
\* ---------------------------------------------------------------------------*/
stmtdef LLVMTypeRef $I {
	addToScope(parentBlock, $0, LLVMTypeRef, LLVMEXBuildAlloca(LLVMPointerType(LLVMEXGetTypeByName("struct.LLVMOpaqueType"), 0), ""));
}

/* ---------------------------------------------------------------------------*\
Name: LLVMTypeRef declaration with initialization
Template: LLVMTypeRef $I = $E<LLVMTypeRef>
Depends on: LLVMTypeRef $I
\* ---------------------------------------------------------------------------*/
stmtdef LLVMTypeRef $I = $E<LLVMTypeRef> {
	LLVMValueRef foo = LLVMEXBuildAlloca(LLVMPointerType(LLVMEXGetTypeByName("struct.LLVMOpaqueType"), 0), "");
	LLVMEXBuildStore($1.codegen(parentBlock), foo);
	addToScope(parentBlock, $0, LLVMTypeRef, foo);
}

stmtdef LLVMTypeRef $I[$E<int>] {
	// Fixed size array:
	/*addToScope(
		parentBlock,
		$0,
		LLVMTypeRef,
		LLVMEXBuildAlloca(
			LLVMArrayType(LLVMPointerType(LLVMEXGetTypeByName("struct.LLVMOpaqueType"), 0), atoi($1.value)),
			""
		)
	);*/

	// Dynamic array:
	LLVMValueRef array = LLVMEXBuildArrayAlloca(
		LLVMPointerType(LLVMEXGetTypeByName("struct.LLVMOpaqueType"), 0),
		$1.codegen(parentBlock),
		""
	);
	LLVMValueRef arrayptr = LLVMEXBuildAlloca(LLVMPointerType(LLVMPointerType(LLVMEXGetTypeByName("struct.LLVMOpaqueType"), 0), 0), "");
	LLVMEXBuildStore(
		array,
		arrayptr
	);
	addToScope(
		parentBlock,
		$0,
		LLVMTypeRefPtr,
		arrayptr
	);
}

exprdef<LLVMTypeRef> $E<LLVMTypeRefPtr>[$E<int>] = $E<LLVMTypeRef> {
	LLVMValueRef var = $0.codegen(parentBlock);
	LLVMValueRef idx = $1.codegen(parentBlock);

	LLVMValueRef gep = LLVMEXBuildInBoundsGEP1(var, idx, "");

	LLVMValueRef expr = $2.codegen(parentBlock);
	LLVMEXBuildStore(
		expr,
		gep
	);
	return expr;
}

/* ---------------------------------------------------------------------------*\
Cast: LLVMMetadataRef to bool
Depends on: return $E
\* ---------------------------------------------------------------------------*/
castdef<bool> $E<LLVMMetadataRef> { return LLVMEXBuildIsNotNull($0.codegen(parentBlock), ""); }

/* ---------------------------------------------------------------------------*\
Name: BuiltinType declaration
Template: BuiltinType $I
\* ---------------------------------------------------------------------------*/
stmtdef BuiltinType $I {
	LLVMValueRef storage = LLVMEXBuildAlloca(LLVMPointerType(LLVMEXGetTypeByName("BuiltinType"), 0), "");
	LLVMSetAlignment(storage, 8);
	addToScope(parentBlock, $0, BuiltinType, storage);
}

/* ---------------------------------------------------------------------------*\
Name: BuiltinType declaration with initialization
Template: BuiltinType $I = $E<BuiltinType>
\* ---------------------------------------------------------------------------*/
stmtdef BuiltinType $I = $E<BuiltinType> {
	LLVMValueRef storage = LLVMEXBuildAlloca(LLVMPointerType(LLVMEXGetTypeByName("BuiltinType"), 0), "");
	LLVMSetAlignment(storage, 8);
	LLVMEXBuildStore($1.codegen(parentBlock), storage);
	addToScope(parentBlock, $0, BuiltinType, storage);
}

exprdef<LLVMTypeRef> $E<BuiltinType>.llvmtype {
	return LLVMEXBuildLoad(LLVMEXBuildStructGEP($0.codegen(parentBlock), 2, "gep"), "llvmtype");
}

exprdef<int> $E<BuiltinType>.align {
	return LLVMEXBuildLoad(LLVMEXBuildStructGEP($0.codegen(parentBlock), 3, "gep"), "align");
}

exprdef<int> $E<BuiltinType>.encoding {
	return LLVMEXBuildLoad(LLVMEXBuildStructGEP($0.codegen(parentBlock), 4, "gep"), "encoding");
}

exprdef<long> $E<BuiltinType>.numbits {
	return LLVMEXBuildLoad(LLVMEXBuildStructGEP($0.codegen(parentBlock), 5, "gep"), "numbits");
}

/* ---------------------------------------------------------------------------*\
Name: Simplified variable declaration
Template: $E<BuiltinType> $I
Notes: This statement does not define debug symbols
\* ---------------------------------------------------------------------------*/
stmtdef $E<BuiltinType> $I {
	BuiltinType type = getconst($0, parentBlock);
	LLVMValueRef storage = LLVMEXBuildAlloca(type.llvmtype, "");
	LLVMSetAlignment(storage, type.align);
	addToScope(parentBlock, $1, type, storage);
}

/* ---------------------------------------------------------------------------*\
Name: Simplified variable declaration with initialization
Template: $E<BuiltinType> $I = $E
Notes: This statement does not define debug symbols and it does not cast the expression type
\* ---------------------------------------------------------------------------*/
stmtdef $E<BuiltinType> $I = $E {
	BuiltinType type = getconst($0, parentBlock);
	LLVMValueRef storage = LLVMEXBuildAlloca(type.llvmtype, "");
	LLVMSetAlignment(storage, type.align);
	LLVMEXBuildStore($2.codegen(parentBlock), storage);
	addToScope(parentBlock, $1, type, storage);
}

/* ---------------------------------------------------------------------------*\
Name: BaseType declaration
Template: BaseType $I
Depends on: none
\* ---------------------------------------------------------------------------*/
stmtdef BaseType $I {
	addToScope(parentBlock, $0, BaseType, LLVMEXBuildAlloca(LLVMPointerType(LLVMEXGetTypeByName("BaseType"), 0), ""));
}

/* ---------------------------------------------------------------------------*\
Name: BaseType declaration with initialization
Template: BaseType $I = $E
TODO: Should be $I = $E<BaseType>
Depends on: BaseType $I
\* ---------------------------------------------------------------------------*/
stmtdef BaseType $I = $E<BaseType> {
	LLVMValueRef foo = LLVMEXBuildAlloca(LLVMPointerType(LLVMEXGetTypeByName("BaseType"), 0), "");
	LLVMEXBuildStore($1.codegen(parentBlock), foo);
	addToScope(parentBlock, $0, BaseType, foo);
}


/* ---------------------------------------------------------------------------*\
|                                                                              |
|                                  AST TYPES                                   |
|                                                                              |
\* ---------------------------------------------------------------------------*/


/* ---------------------------------------------------------------------------*\
Name: ExprAST declaration
Template: ExprAST $I
Depends on: none
\* ---------------------------------------------------------------------------*/
stmtdef ExprAST $I {
	addToScope(parentBlock, $0, ExprAST, LLVMEXBuildAlloca(LLVMPointerType(LLVMEXGetTypeByName("class.ExprAST"), 0), ""));
}

/* ---------------------------------------------------------------------------*\
Name: ExprAST declaration with initialization
Template: ExprAST $I = $E
Depends on: ExprAST $I
\* ---------------------------------------------------------------------------*/
stmtdef ExprAST $I = $E<ExprAST> {
	LLVMValueRef foo = LLVMEXBuildAlloca(LLVMPointerType(LLVMEXGetTypeByName("class.ExprAST"), 0), "");
	LLVMEXBuildStore($1.codegen(parentBlock), foo);
	addToScope(parentBlock, $0, ExprAST, foo);
}

/* ---------------------------------------------------------------------------*\
Name: LiteralExprAST declaration
Template: LiteralExprAST $I
Depends on: none
\* ---------------------------------------------------------------------------*/
stmtdef LiteralExprAST $I {
	addToScope(parentBlock, $0, LiteralExprAST, LLVMEXBuildAlloca(LLVMPointerType(LLVMEXGetTypeByName("class.LiteralExprAST"), 0), ""));
}

/* ---------------------------------------------------------------------------*\
Name: LiteralExprAST declaration with initialization
Template: LiteralExprAST $I = $E
Depends on: LiteralExprAST $I
\* ---------------------------------------------------------------------------*/
stmtdef LiteralExprAST $I = $E<LiteralExprAST> {
	LLVMValueRef foo = LLVMEXBuildAlloca(LLVMPointerType(LLVMEXGetTypeByName("class.LiteralExprAST"), 0), "");
	LLVMEXBuildStore($1.codegen(parentBlock), foo);
	addToScope(parentBlock, $0, LiteralExprAST, foo);
}

/* ---------------------------------------------------------------------------*\
Name: IdExprAST declaration
Template: IdExprAST $I
Depends on: none
\* ---------------------------------------------------------------------------*/
stmtdef IdExprAST $I {
	addToScope(parentBlock, $0, IdExprAST, LLVMEXBuildAlloca(LLVMPointerType(LLVMEXGetTypeByName("class.IdExprAST"), 0), ""));
}

/* ---------------------------------------------------------------------------*\
Name: IdExprAST declaration with initialization
Template: IdExprAST $I = $E
Depends on: IdExprAST $I
\* ---------------------------------------------------------------------------*/
stmtdef IdExprAST $I = $E<IdExprAST> {
	LLVMValueRef foo = LLVMEXBuildAlloca(LLVMPointerType(LLVMEXGetTypeByName("class.IdExprAST"), 0), "");
	LLVMEXBuildStore($1.codegen(parentBlock), foo);
	addToScope(parentBlock, $0, IdExprAST, foo);
}

/* ---------------------------------------------------------------------------*\
Name: BlockExprAST declaration
Template: BlockExprAST $I
Depends on: none
\* ---------------------------------------------------------------------------*/
stmtdef BlockExprAST $I {
	addToScope(parentBlock, $0, BlockExprAST, LLVMEXBuildAlloca(LLVMPointerType(LLVMEXGetTypeByName("class.BlockExprAST"), 0), ""));
}

/* ---------------------------------------------------------------------------*\
Name: BlockExprAST declaration with initialization
Template: BlockExprAST $I = $E
Depends on: BlockExprAST $I
\* ---------------------------------------------------------------------------*/
stmtdef BlockExprAST $I = $E<BlockExprAST> {
	LLVMValueRef foo = LLVMEXBuildAlloca(LLVMPointerType(LLVMEXGetTypeByName("class.BlockExprAST"), 0), "");
	LLVMEXBuildStore($1.codegen(parentBlock), foo);
	addToScope(parentBlock, $0, BlockExprAST, foo);
}

exprdef<Location> $E<ExprAST>.loc {
	LLVMValueRef args[1];
	args[0] = $0.codegen(parentBlock);
	return LLVMEXBuildCall(getExprLoc, args, 1, "");
}

exprdef<int> $E<ExprListAST>.size {
	LLVMValueRef args[1];
	args[0] = $0.codegen(parentBlock);
	return LLVMEXBuildTrunc(
		LLVMEXBuildCall(getExprListASTSize, args, 1, ""),
		LLVMInt32Type(),
		""
	);
}

exprdef<string> $E<LiteralExprAST>.value {
	LLVMValueRef args[1];
	args[0] = $0.codegen(parentBlock);
	return LLVMEXBuildCall(getLiteralExprASTValue, args, 1, "");
}

exprdef<string> $E<IdExprAST>.name {
	LLVMValueRef args[1];
	args[0] = $0.codegen(parentBlock);
	return LLVMEXBuildCall(getIdExprASTName, args, 1, "");
}

exprdef<BlockExprAST> $E<BlockExprAST>.parent {
	LLVMValueRef args[1];
	args[0] = $0.codegen(parentBlock);
	return LLVMEXBuildCall(getBlockExprASTParent, args, 1, "");
}

exprdef<BlockExprAST> $E<BlockExprAST>.parent = $E<BlockExprAST> {
	LLVMValueRef args[2];
	args[0] = $0.codegen(parentBlock);
	args[1] = $1.codegen(parentBlock);
	return LLVMEXBuildCall(setBlockExprASTParent, args, 2, "");
}

/* ---------------------------------------------------------------------------*\
Cast: LiteralExprAST to ExprAST
Depends on: return $E
\* ---------------------------------------------------------------------------*/
inhtdef<ExprAST> $E<LiteralExprAST> {
	return LLVMEXBuildBitCast(
		$0.codegen(parentBlock),
		LLVMPointerType(LLVMEXGetTypeByName("class.ExprAST"), 0),
		""
	);
}

/* ---------------------------------------------------------------------------*\
Cast: IdExprAST to ExprAST
Depends on: return $E
\* ---------------------------------------------------------------------------*/
inhtdef<ExprAST> $E<IdExprAST> {
	return LLVMEXBuildBitCast(
		$0.codegen(parentBlock),
		LLVMPointerType(LLVMEXGetTypeByName("class.ExprAST"), 0),
		""
	);
}

/* ---------------------------------------------------------------------------*\
Cast: BlockExprAST to ExprAST
Depends on: return $E
\* ---------------------------------------------------------------------------*/
inhtdef<ExprAST> $E<BlockExprAST> {
	return LLVMEXBuildBitCast(
		$0.codegen(parentBlock),
		LLVMPointerType(LLVMEXGetTypeByName("class.ExprAST"), 0),
		""
	);
}

/* ---------------------------------------------------------------------------*\
Cast: ExprAST to bool
Depends on: return $E
\* ---------------------------------------------------------------------------*/
castdef<bool> $E<ExprAST> { return LLVMEXBuildIsNotNull($0.codegen(parentBlock), ""); }


/* ---------------------------------------------------------------------------*\
|                                                                              |
|                                NUMERIC TYPES                                 |
|                                                                              |
\* ---------------------------------------------------------------------------*/


/* ---------------------------------------------------------------------------*\
Name: Logical NOT
Template: !$E<bool>
Depends on: return $E
\* ---------------------------------------------------------------------------*/
exprdef<bool> !$E<bool> {
	return LLVMEXBuildNot($0.codegen(parentBlock), "not");
}

/* ---------------------------------------------------------------------------*\
Name: Logical AND
Template: $E<bool> && $E<bool>
Depends on: return $E
\* ---------------------------------------------------------------------------*/
exprdef<bool> $E<bool> && $E<bool> {
	return LLVMEXBuildAnd($0.codegen(parentBlock), $1.codegen(parentBlock), "and");
}

/* ---------------------------------------------------------------------------*\
Name: Logical OR
Template: $E<bool> || $E<bool>
Depends on: return $E
\* ---------------------------------------------------------------------------*/
exprdef<bool> $E<bool> || $E<bool> {
	return LLVMEXBuildOr($0.codegen(parentBlock), $1.codegen(parentBlock), "or");
}

exprdef<bool> $E<bool> == $E<bool> {
	return LLVMEXBuildICmp(LLVMIntEQ, $0.codegen(parentBlock), $1.codegen(parentBlock), "cmp");
}

exprdef<bool> $E<bool> != $E<bool> {
	return LLVMEXBuildICmp(LLVMIntNE, $0.codegen(parentBlock), $1.codegen(parentBlock), "cmp");
}

/* ---------------------------------------------------------------------------*\
Name: Integer addition
Template: $E<int> + $E<int>
Depends on: return $E
\* ---------------------------------------------------------------------------*/
// exprdef<int> $E<int> + $E<int> {
// 	return LLVMEXBuildAdd($0.codegen(parentBlock), $1.codegen(parentBlock), "add");
// }
exprdef<int> $E<int> + $E<int> {
	return LLVMEXBuildAdd($0.codegen(parentBlock), $1.codegen(parentBlock), "add");
}

/* ---------------------------------------------------------------------------*\
Name: Integer subtraction
Template: $E<int> - $E<int>
Depends on: return $E
\* ---------------------------------------------------------------------------*/
exprdef<int> $E<int> - $E<int> {
	return LLVMEXBuildSub($0.codegen(parentBlock), $1.codegen(parentBlock), "sub");
}

/* ---------------------------------------------------------------------------*\
Name: Integer multiplication
Template: $E<int> * $E<int>
Depends on: return $E
\* ---------------------------------------------------------------------------*/
exprdef<int> $E<int> * $E<int> {
	return LLVMEXBuildMul($0.codegen(parentBlock), $1.codegen(parentBlock), "mul");
}

/* ---------------------------------------------------------------------------*\
Name: Integer division
Template: $E<int> / $E<int>
Depends on: return $E
\* ---------------------------------------------------------------------------*/
exprdef<int> $E<int> / $E<int> {
	return LLVMEXBuildSDiv($0.codegen(parentBlock), $1.codegen(parentBlock), "div");
}

exprdef<bool> $E<int> == $E<int> {
	return LLVMEXBuildICmp(LLVMIntEQ, $0.codegen(parentBlock), $1.codegen(parentBlock), "cmp");
}

exprdef<bool> $E<int> != $E<int> {
	return LLVMEXBuildICmp(LLVMIntNE, $0.codegen(parentBlock), $1.codegen(parentBlock), "cmp");
}

/* ---------------------------------------------------------------------------*\
Name: Long bitwise AND
Template: $E<long> & $E<long>
Depends on: return $E
\* ---------------------------------------------------------------------------*/
exprdef<long> $E<long> & $E<long> {
	return LLVMEXBuildAnd($0.codegen(parentBlock), $1.codegen(parentBlock), "and");
}

exprdef<bool> $E<long> == $E<long> {
	return LLVMEXBuildICmp(LLVMIntEQ, $0.codegen(parentBlock), $1.codegen(parentBlock), "cmp");
}

exprdef<bool> $E<long> != $E<long> {
	return LLVMEXBuildICmp(LLVMIntNE, $0.codegen(parentBlock), $1.codegen(parentBlock), "cmp");
}

/* ---------------------------------------------------------------------------*\
Name: 64 bit float addition
Template: $E<double> + $E<double>
Depends on: return $E
\* ---------------------------------------------------------------------------*/
exprdef<double> $E<double> + $E<double> {
	return LLVMEXBuildFAdd($0.codegen(parentBlock), $1.codegen(parentBlock), "fadd");
}

/* ---------------------------------------------------------------------------*\
Name: 64 bit float multiplication
Template: $E<double> * $E<double>
Depends on: return $E
\* ---------------------------------------------------------------------------*/
exprdef<double> $E<double> * $E<double> {
	return LLVMEXBuildFMul($0.codegen(parentBlock), $1.codegen(parentBlock), "fmul");
}

exprdef<bool> $E<double> == $E<double> { //TODO: Untested
	return LLVMEXBuildFCmp(LLVMRealOEQ, $0.codegen(parentBlock), $1.codegen(parentBlock), "fcmp");
}

exprdef<bool> $E<double> != $E<double> { //TODO: Untested
	return LLVMEXBuildFCmp(LLVMRealONE, $0.codegen(parentBlock), $1.codegen(parentBlock), "fcmp");
}

/* ---------------------------------------------------------------------------*\
Cast: int to double
Depends on: return $E
\* ---------------------------------------------------------------------------*/
castdef<double> $E<int> {
	return LLVMEXBuildSIToFP(
		$0.codegen(parentBlock),
		LLVMDoubleType(),
		""
	);
}

/* ---------------------------------------------------------------------------*\
Cast: double to int
Depends on: return $E
\* ---------------------------------------------------------------------------*/
castdef<int> $E<double> {
	return LLVMEXBuildFPToSI(
		$0.codegen(parentBlock),
		LLVMInt32Type(),
		""
	);
}

/* ---------------------------------------------------------------------------*\
Cast: double to long
Depends on: return $E
\* ---------------------------------------------------------------------------*/
castdef<long> $E<double> {
	return LLVMEXBuildFPToSI(
		$0.codegen(parentBlock),
		LLVMInt64Type(),
		""
	);
}

/* ---------------------------------------------------------------------------*\
Cast: bool to int
Depends on: return $E
\* ---------------------------------------------------------------------------*/
castdef<int> $E<bool> {
	return LLVMEXBuildZExt(
		$0.codegen(parentBlock),
		LLVMInt32Type(),
		""
	);
}

/* ---------------------------------------------------------------------------*\
Cast: long to int
Depends on: return $E
\* ---------------------------------------------------------------------------*/
castdef<int> $E<long> {
	return LLVMEXBuildTrunc(
		$0.codegen(parentBlock),
		LLVMInt32Type(),
		""
	);
}

/* ---------------------------------------------------------------------------*\
Cast: int to bool
Depends on: return $E
\* ---------------------------------------------------------------------------*/
castdef<bool> $E<int> { return LLVMEXBuildIsNotNull($0.codegen(parentBlock), ""); }

/* ---------------------------------------------------------------------------*\
Cast: int to long
Depends on: return $E
\* ---------------------------------------------------------------------------*/
castdef<long> $E<int> {
	return LLVMEXBuildSExt(
		$0.codegen(parentBlock),
		LLVMInt64Type(),
		""
	);
}


/* ---------------------------------------------------------------------------*\
|                                                                              |
|                                 STRING TYPES                                 |
|                                                                              |
\* ---------------------------------------------------------------------------*/


// /* ---------------------------------------------------------------------------*\
// Name: String declaration
// Template: string $I
// \* ---------------------------------------------------------------------------*/
// stmtdef string $I {
// 	LLVMValueRef storage = LLVMEXBuildAlloca(LLVMPointerType(LLVMInt8Type(), 0), "");
// 	LLVMSetAlignment(storage, 1);
// 	addToScope(parentBlock, $0, string, storage);
// }

// /* ---------------------------------------------------------------------------*\
// Name: String declaration with initialization
// Template: string $I = $E<string>
// \* ---------------------------------------------------------------------------*/
// stmtdef string $I = $E<string> {
// 	LLVMValueRef storage = LLVMEXBuildAlloca(LLVMPointerType(LLVMInt8Type(), 0), "");
// 	LLVMSetAlignment(storage, 1);
// 	LLVMEXBuildStore($1.codegen(parentBlock), storage);
// 	addToScope(parentBlock, $0, string, storage);
// }


/* ---------------------------------------------------------------------------*\
|                                                                              |
|                                  MISC TYPES                                  |
|                                                                              |
\* ---------------------------------------------------------------------------*/


/* ---------------------------------------------------------------------------*\
Name: BuiltinType array declaration
Template: BuiltinType $I[$E<int>]
\* ---------------------------------------------------------------------------*/
stmtdef BuiltinType $I[$E<int>] {
	LLVMValueRef array = LLVMEXBuildArrayAlloca(LLVMPointerType(LLVMEXGetTypeByName("BuiltinType"), 0), $1.codegen(parentBlock), "");
	LLVMValueRef arrayptr = LLVMEXBuildAlloca(LLVMPointerType(LLVMPointerType(LLVMEXGetTypeByName("BuiltinType"), 0), 0), "");
	LLVMEXBuildStore(array, arrayptr);
	addToScope(parentBlock, $0, BuiltinTypePtr, arrayptr);
}

/* ---------------------------------------------------------------------------*\
Name: BuiltinType array element setter
Template: $E<BuiltinTypePtr>[$E<int>] = $E<BuiltinType>
\* ---------------------------------------------------------------------------*/
exprdef<BuiltinType> $E<BuiltinTypePtr>[$E<int>] = $E<BuiltinType> {
	LLVMValueRef var = $0.codegen(parentBlock);
	LLVMValueRef idx = $1.codegen(parentBlock);

	LLVMValueRef gep = LLVMEXBuildInBoundsGEP1(var, idx, "");

	LLVMValueRef expr = $2.codegen(parentBlock);
	LLVMEXBuildStore(expr, gep);
	return expr;
}

/* ---------------------------------------------------------------------------*\
Name: BuiltinType array element getter
Template: $E<BuiltinTypePtr>[$E<int>]
\* ---------------------------------------------------------------------------*/
exprdef<BuiltinType> $E<BuiltinTypePtr>[$E<int>] {
	LLVMValueRef var = $0.codegen(parentBlock);
	LLVMValueRef idx = $1.codegen(parentBlock);

	LLVMValueRef gep = LLVMEXBuildInBoundsGEP1(var, idx, "");
	LLVMValueRef val = LLVMEXBuildLoad(gep, "");
	LLVMSetAlignment(val, 8);
	return val;
}

exprdef<bool> $E<BuiltinType> == $E<BuiltinType> {
	return LLVMEXBuildICmp(LLVMIntEQ, $0.codegen(parentBlock), $1.codegen(parentBlock), "cmp");
}

exprdef<bool> $E<BuiltinType> != $E<BuiltinType> {
	return LLVMEXBuildICmp(LLVMIntNE, $0.codegen(parentBlock), $1.codegen(parentBlock), "cmp");
}

exprdef<bool> $E<BaseType> == $E<BaseType> {
	return LLVMEXBuildICmp(LLVMIntEQ, $0.codegen(parentBlock), $1.codegen(parentBlock), "cmp");
}

exprdef<bool> $E<BaseType> != $E<BaseType> {
	return LLVMEXBuildICmp(LLVMIntNE, $0.codegen(parentBlock), $1.codegen(parentBlock), "cmp");
}

/* ---------------------------------------------------------------------------*\
Cast: BuiltinType to BaseType
Depends on: return $E
\* ---------------------------------------------------------------------------*/
inhtdef<BaseType> $E<BuiltinType> {
	return LLVMEXBuildBitCast(
		$0.codegen(parentBlock),
		LLVMPointerType(LLVMEXGetTypeByName("BaseType"), 0),
		""
	);
}


/* ---------------------------------------------------------------------------*\
|                                                                              |
|                           FLOW CONTROL STATEMENTS                            |
|                                                                              |
\* ---------------------------------------------------------------------------*/


/* ---------------------------------------------------------------------------*\
Name: If statement
Template: if($E<bool>) $S
Depends on: LLVMValueRef $I = $E<LLVMValueRef>
            LLVMBasicBlockRef $I = $E<LLVMBasicBlockRef>
\* ---------------------------------------------------------------------------*/
stmtdef if($E<bool>) $S {
	LLVMValueRef ifCond = $0.codegen(parentBlock);
	LLVMBasicBlockRef ifBlock = LLVMAppendBasicBlock(function, "ifBlock");
	LLVMBasicBlockRef elseBlock = LLVMAppendBasicBlock(function, "elseBlock");
	LLVMEXBuildCondBr(ifCond, ifBlock, elseBlock);

	LLVMEXPositionBuilder(ifBlock);
	$1.codegen(parentBlock);
	LLVMEXBuildBr(elseBlock);

	LLVMEXPositionBuilder(elseBlock);
}

/* ---------------------------------------------------------------------------*\
Name: If-else statement
Template: if($E<bool>) $S else $S
Depends on: LLVMValueRef $I = $E<LLVMValueRef>
            LLVMBasicBlockRef $I = $E<LLVMBasicBlockRef>
\* ---------------------------------------------------------------------------*/
stmtdef if($E<bool>) $S else $S {
	LLVMValueRef ifCond = $0.codegen(parentBlock);
	LLVMBasicBlockRef ifBlock = LLVMAppendBasicBlock(function, "ifBlock");
	LLVMBasicBlockRef elseBlock = LLVMAppendBasicBlock(function, "elseBlock");
	LLVMBasicBlockRef endIfBlock = LLVMAppendBasicBlock(function, "endIfBlock");
	LLVMEXBuildCondBr(ifCond, ifBlock, elseBlock);

	LLVMEXPositionBuilder(ifBlock);
	$1.codegen(parentBlock);
	LLVMEXBuildBr(endIfBlock);

	LLVMEXPositionBuilder(elseBlock);
	$2.codegen(parentBlock);
	LLVMEXBuildBr(endIfBlock);

	LLVMEXPositionBuilder(endIfBlock);
}

stmtdef for(int $I = $E<int>; $E; $E) $B {
	// Inherent global scope into for block scope
	$4.parent = parentBlock;

	// Allocate loop variable
	LLVMValueRef loopVar = LLVMEXBuildAlloca(LLVMInt32Type(), "");
	LLVMSetAlignment(loopVar, 4);
	LLVMSetAlignment(LLVMEXBuildStore($1.codegen(parentBlock), loopVar), 4);
	addToScope($4, $0, int, loopVar);

	// Reresolve condition and update expressions to take loop variable into account
	resolveExprAST($4, $2);
	resolveExprAST($4, $3);

	// Create basic blocks
	LLVMBasicBlockRef forCondBlock = LLVMAppendBasicBlock(function, "forCondBlock");
	LLVMBasicBlockRef forBlock = LLVMAppendBasicBlock(function, "forBlock");
	LLVMBasicBlockRef endForBlock = LLVMAppendBasicBlock(function, "endForBlock");
	LLVMEXBuildBr(forCondBlock);

	// Exit for loop if condition isn't met
	LLVMEXPositionBuilder(forCondBlock);
	LLVMValueRef forCond = $2.codegen($4); // Codegen condition in loop block scope
	LLVMEXBuildCondBr(forCond, forBlock, endForBlock);

	LLVMEXPositionBuilder(forBlock);
	$4.codegen(parentBlock); // Codegen loop block in global scope
	$3.codegen($4); // Codegen update expression in loop block scope

	// Repeat for loop
	LLVMEXBuildBr(forCondBlock);

	LLVMEXPositionBuilder(endForBlock);
}

stmtdef while ($E<bool>) $S {
	// Create basic blocks
	LLVMBasicBlockRef whileBlock = LLVMAppendBasicBlock(function, "whileBlock");
	LLVMBasicBlockRef endWhileBlock = LLVMAppendBasicBlock(function, "endWhileBlock");

	// Skip while loop if condition isn't met
	LLVMValueRef whileCond = $0.codegen(parentBlock);
	LLVMEXBuildCondBr(whileCond, whileBlock, endWhileBlock);

	LLVMEXPositionBuilder(whileBlock);
	$1.codegen(parentBlock); // Codegen loop block

	// Repeat while loop if condition is met
	whileCond = $0.codegen(parentBlock);
	LLVMEXBuildCondBr(whileCond, whileBlock, endWhileBlock);

	LLVMEXPositionBuilder(endWhileBlock);
}


/* ---------------------------------------------------------------------------*\
|                                                                              |
|                               MISC STATEMENTS                                |
|                                                                              |
\* ---------------------------------------------------------------------------*/


/* ---------------------------------------------------------------------------*\
Name: File-scope return statement
Template: return $E
Depends on: none
\* ---------------------------------------------------------------------------*/
stmtdef return $E<int> {
	LLVMEXBuildRet($0.codegen(parentBlock));
}

/* ---------------------------------------------------------------------------*\
Name: Context-free block statement
Template: $B
Depends on: none
\* ---------------------------------------------------------------------------*/
stmtdef $B {
	$0.codegen(parentBlock);
}

/* ---------------------------------------------------------------------------*\
Name: Type lookup
Template: gettype($E<ExprAST>, $E<BlockExprAST>)
TODO: Return BaseType instead of BuiltinType
\* ---------------------------------------------------------------------------*/
exprdef<BuiltinType> gettype($E<ExprAST>, $E<BlockExprAST>) {
	LLVMValueRef args[2];
	args[0] = $0.codegen(parentBlock);
	args[1] = $1.codegen(parentBlock);
	return LLVMEXBuildBitCast(LLVMEXBuildCall(getType, args, 2, ""), BuiltinType.llvmtype, "");
}

/* ---------------------------------------------------------------------------*\
Name: Throw statement
Template: throw $E<string>
\* ---------------------------------------------------------------------------*/
stmtdef throw $E<string> {
	exprdef<long> long($E<ExprAST>) { return LLVMEXBuildPtrToInt($0.codegen(parentBlock), long.llvmtype, ""); }

	LLVMValueRef args[2];
	args[0] = $0.codegen(parentBlock);
	args[1] = LLVMEXBuildIntToPtr(LLVMConstInt(LLVMInt64Type(), long($0), 0), ExprAST.llvmtype, "");
	LLVMEXBuildCall(raiseCompileError, args, 2, "");
}

/* ---------------------------------------------------------------------------*\
Name: Compile exception statement
Template: throw CompileError($E<string>, $E<ExprAST>)
\* ---------------------------------------------------------------------------*/
stmtdef throw CompileError($E<string>, $E<ExprAST>) {
	LLVMValueRef args[2];
	args[0] = $0.codegen(parentBlock);
	args[1] = $1.codegen(parentBlock);
	LLVMEXBuildCall(raiseCompileError, args, 2, "");
}

/* ---------------------------------------------------------------------------*\
Name: Unimplemented single-statement for-loop error
Template: for(int $I = $E<int>; $E; $E) $S
\* ---------------------------------------------------------------------------*/
stmtdef for(int $I = $E<int>; $E; $E) $S {
	throw CompileError("single-statement for-loop not supported. Use `for (...) {...}` instead!", $4);
}

/* ---------------------------------------------------------------------------*\
Name: Function declaration
Template: $E<BuiltinType> $I($E<BuiltinType> $I, ...)
\* ---------------------------------------------------------------------------*/
stmtdef $E<BuiltinType> $I($E<BuiltinType> $I, ...) {
	BuiltinType returnType = getconst($0, parentBlock);
	string funcName = $1.name;
	int numArgs = $2.size;

	BuiltinType argTypes[numArgs];
	for (int i = 0; i != numArgs; i = i + 1)
	{
		argTypes[i] = getconst($2[i], parentBlock);
	}

	defineFunction(parentBlock, funcName, returnType, argTypes, numArgs, 0);
}

/* ---------------------------------------------------------------------------*\
Name: Function declaration without parameter names
Template: $E<BuiltinType> $I($E<BuiltinType>, ...)
\* ---------------------------------------------------------------------------*/
stmtdef $E<BuiltinType> $I($E<BuiltinType>, ...) {
	BuiltinType returnType = getconst($0, parentBlock);
	string funcName = $1.name;
	int numArgs = $2.size;

	BuiltinType argTypes[numArgs];
	for (int i = 0; i != numArgs; i = i + 1)
	{
		argTypes[i] = getconst($2[i], parentBlock);
	}

	// Handle $E<BuiltinType> $I(void)
	if (numArgs == 1 && argTypes[0] == void)
		numArgs = numArgs - 1;

	defineFunction(parentBlock, funcName, returnType, argTypes, numArgs, 0);
}

/* ---------------------------------------------------------------------------*\
Name: Variable argument function declaration
Template: $E<BuiltinType> $I($E<BuiltinType> $I, ..., $V)
\* ---------------------------------------------------------------------------*/
stmtdef $E<BuiltinType> $I($E<BuiltinType> $I, ..., $V) {
	BuiltinType returnType = getconst($0, parentBlock);
	string funcName = $1.name;
	int numArgs = $2.size;

	BuiltinType argTypes[numArgs];
	for (int i = 0; i != numArgs; i = i + 1)
	{
		argTypes[i] = getconst($2[i], parentBlock);
	}

	defineFunction(parentBlock, funcName, returnType, argTypes, numArgs, 1);
}

// /* ---------------------------------------------------------------------------*\
// Name: Variable argument function declaration without parameter names
// Template: $E<BuiltinType> $I($E<BuiltinType>, ..., $V)
// TODO: This breaks!
// \* ---------------------------------------------------------------------------*/
// stmtdef $E<BuiltinType> $I($E<BuiltinType>, ..., $V) {
// 	BuiltinType returnType = getconst($0, parentBlock);
// 	string funcName = $1.name;
// 	int numArgs = $2.size;

// 	BuiltinType argTypes[numArgs];
// 	for (int i = 0; i != numArgs; i = i + 1)
// 	{
// 		argTypes[i] = getconst($2[i], parentBlock);
// 	}

// 	defineFunction(parentBlock, funcName, returnType, argTypes, numArgs, 1);
// }

/* ---------------------------------------------------------------------------*\
Name: Variable declaration
Template: $E<BuiltinType> $I
\* ---------------------------------------------------------------------------*/
stmtdef $E<BuiltinType> $I {
	long strlen(string str);

	BuiltinType type = getconst($0, parentBlock);
	string typeName = getTypeName(type);
	LLVMValueRef storage = LLVMEXAddGlobal(type.llvmtype, $1.name);
	LLVMSetInitializer(storage, LLVMConstNull(type.llvmtype));
	LLVMSetAlignment(storage, type.align);
	addToScope(parentBlock, $1, type, storage);

	if (dfile && type.encoding != 0)
	{
		LLVMEXDIBuilderInsertDeclareAtEnd(
			storage,
			LLVMEXDIBuilderCreateAutoVariable(
				LLVMGetSubprogram(function),
				$1.name,
				strlen($1.name),
				dfile,
				getExprLine($1),
				LLVMEXDIBuilderCreateBasicType(typeName, strlen(typeName), type.numbits, type.encoding, 0),
				0,
				0,
				0
			),
			LLVMEXDIBuilderCreateExpression(),
			LLVMEXDIBuilderCreateDebugLocation(getExprLine($1), getExprColumn($1), LLVMGetSubprogram(function)),
			LLVMEXGetInsertBlock()
		);
	}
}

/* ---------------------------------------------------------------------------*\
Name: Variable declaration with initialization
Template: $E<BuiltinType> $I = $E
\* ---------------------------------------------------------------------------*/
stmtdef $E<BuiltinType> $I = $E {
	string malloc(long size);
	string strcpy(string destination, string source);
	string strcat(string destination, string source);
	long strlen(string str);

	BuiltinType varType = getconst($0, parentBlock);
	string varTypeName = getTypeName(varType);
	BuiltinType exprType = gettype($2, parentBlock);
	ExprAST expr = $2;
	if (varType != exprType)
	{
		expr = lookupCast(parentBlock, expr, varType);
		if (!expr)
		{
			string err = malloc(256);
			strcpy(err, "cannot convert <");
			strcat(err, getTypeName(exprType));
			strcat(err, "> to <");
			strcat(err, varTypeName);
			strcat(err, "> in initialization");
			throw CompileError(err, $0);
		}
	}

	LLVMValueRef storage = LLVMEXAddGlobal(varType.llvmtype, $1.name);
	LLVMSetInitializer(storage, LLVMConstNull(varType.llvmtype));
	LLVMSetAlignment(storage, varType.align);
	LLVMEXBuildStore(expr.codegen(parentBlock), storage);
	addToScope(parentBlock, $1, varType, storage);

	if (dfile && varType.encoding != 0)
	{
		LLVMEXDIBuilderInsertDeclareAtEnd(
			storage,
			LLVMEXDIBuilderCreateAutoVariable(
				LLVMGetSubprogram(function),
				$1.name,
				strlen($1.name),
				dfile,
				getExprLine($1),
				LLVMEXDIBuilderCreateBasicType(varTypeName, strlen(varTypeName), varType.numbits, varType.encoding, 0),
				0,
				0,
				0
			),
			LLVMEXDIBuilderCreateExpression(),
			LLVMEXDIBuilderCreateDebugLocation(getExprLine($1), getExprColumn($1), LLVMGetSubprogram(function)),
			LLVMEXGetInsertBlock()
		);
	}
}

/* ---------------------------------------------------------------------------*\
Name: Variable assignment
Template: $I = $E
\* ---------------------------------------------------------------------------*/
exprdef<gettype($0, parentBlock)> $I = $E {
	string malloc(long size);
	string strcpy(string destination, string source);
	string strcat(string destination, string source);

	BaseType varType = LookupScopeType(parentBlock, $0);
	BuiltinType exprType = gettype($1, parentBlock);
	ExprAST expr = $1;
	if (varType != exprType)
	{
		expr = lookupCast(parentBlock, expr, varType);
		if (!expr)
		{
			string err = malloc(256);
			strcpy(err, "cannot convert <");
			strcat(err, getTypeName(exprType));
			strcat(err, "> to <");
			strcat(err, getTypeName(varType));
			strcat(err, "> in assignment");
			throw CompileError(err, $0);
		}
	}

	LLVMValueRef varValue = LookupScope(parentBlock, $0);
	if (!varValue)
	{
		string err = malloc(256);
		strcpy(err, "`");
		strcat(err, $0.name);
		strcat(err, "` was not declared in this scope");
		throw CompileError(err, $0);
	}

	LLVMValueRef exprValue = expr.codegen(parentBlock);
	LLVMEXBuildStore(exprValue, varValue);
	return exprValue;
}