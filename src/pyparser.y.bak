%require "3.3"
%debug
%defines
%language "C++"
%locations
%define api.parser.class {PyParser}
%define api.value.type variant
%parse-param {PyLexer& scanner}
%parse-param {const char* filename}
%parse-param {BlockExprAST** rootBlock}

%code requires{
	class PyLexer;
}

%{
#include <vector>
#include <stdlib.h>
#include <fstream>
#include "../src/pyparser.h"

#undef yylex
#define yylex scanner.yylex

#define getloc(b, e) Location{filename, (unsigned)b.begin.line, (unsigned)b.begin.column, (unsigned)e.end.line, (unsigned)e.end.column}
%}

%token ELLIPSIS // ...
%token EQ NE GEQ LEQ GR LE DM SR INC DEC RS LS AND OR IDIV CADD CSUB CMUL CMML CDIV CMOD CAND COR CXOR CLS CRS CPOW CIDV // Operators
%token AWT NEW FOR IN // Keywords
%token NEWLINE INDENT OUTDENT // Language specific tokens
%token<const char*> LITERAL ID PLCHLD2
%token<char> PLCHLD1
%token<int> PARAM
%type<BlockExprAST*> block
%type<std::vector<ExprAST*>*> stmt_string
%type<ExprListAST*> stmt
%type<ExprListAST*> optional_stmt expr_string optional_expr_string expr_idx
%type<ExprAST*> id_or_plchld expr _expr

%start file
%right CADD CSUB CMUL CMML CDIV CMOD CAND COR CXOR CLS CRS CPOW CIDV
%left FOR
%left IN
%left ','
%right '=' ':'
%left OR
%left AND
%left '&'
%left EQ NE
%left GEQ LEQ RS LS '>' '<'
%left '+' '-'
%left '*' '/' '%' IDIV
%right AWT NEW REF
%right DCOR
%left '.' CALL SUBSCRIPT TPLT DM
%left ENC

%%

file
	: stmt_string { *rootBlock = new BlockExprAST(getloc(@1, @1), $1); }
	| %empty { *rootBlock = new BlockExprAST({0}, new std::vector<ExprAST*>()); } // Empty file
;

block
	: INDENT stmt_string OUTDENT { $$ = new BlockExprAST(Location{filename, (unsigned)@1.begin.line, (unsigned)@1.begin.column, $2->back()->loc.end_line, $2->back()->loc.end_col}, $2); }
;

stmt_string
	: stmt NEWLINE { $$ = &$1->exprs; $$->push_back(new StopExprAST(Location{filename, (unsigned)@1.end.line, (unsigned)@1.end.column, (unsigned)@1.end.line, (unsigned)@1.end.column})); }
	// | stmt ':' NEWLINE block { $$ = &$1->exprs; $$->push_back($4); }
	| stmt_string NEWLINE { $$ = $1; } // Blank line
	| stmt_string stmt NEWLINE { ($$ = $1)->insert($1->end(), $2->cbegin(), $2->cend()); $$->push_back(new StopExprAST(Location{filename, (unsigned)@2.end.line, (unsigned)@2.end.column, (unsigned)@2.end.line, (unsigned)@2.end.column})); }
	// | stmt_string stmt ':' NEWLINE block { ($$ = $1)->insert($1->end(), $2->cbegin(), $2->cend()); $$->push_back($5); }
;

stmt
	: expr	{
								ExprListAST* stmt = new ExprListAST('\0');
								stmt->exprs.push_back($1);
								$$ = stmt;
							}
	| stmt expr	{
								ExprListAST* stmt = $1;
								stmt->exprs.push_back($2);
								$$ = stmt;
							}
	| stmt '=' expr	{
								ExprListAST* stmt = $1;
								const Location& loc = Location{filename, stmt->exprs.back()->loc.begin_line, stmt->exprs.back()->loc.begin_col, (unsigned)@3.end.line, (unsigned)@3.end.column};
								stmt->exprs.back() = new BinOpExprAST(loc, (int)'=', "=", stmt->exprs.back(), $3);
								$$ = stmt;
							}
;

optional_stmt
	: %empty { $$ = new ExprListAST('\0'); }
	//| stmt { $$ = $1; }
	| stmt { $$ = $1->exprs.front()->exprtype == ExprAST::ExprType::LIST ? (ExprListAST*)$1->exprs.front() : new ExprListAST('\0', { $1->exprs.front() }); }
;

expr_string
	// : _expr { $$ = new ExprListAST('\0'); $$->exprs.push_back($1); }
	// | expr_string _expr { ($$ = $1)->exprs.push_back($2); }
	: LITERAL { $$ = new ExprListAST('\0'); $$->exprs.push_back(new LiteralExprAST(getloc(@1, @1), $1)); } //DELETE
	| expr_string LITERAL { ($$ = $1)->exprs.push_back(new LiteralExprAST(getloc(@2, @2), $2)); } //DELETE
	| expr_string ELLIPSIS { ($$ = $1)->exprs.back() = new EllipsisExprAST(getloc(@2, @2), $1->exprs.back()); }
;

optional_expr_string
	: %empty { $$ = new ExprListAST('\0'); }
	| expr_string { $$ = $1; }
;

// expr_idx
// 	: expr_string { $$ = new ExprListAST(':'); $$->exprs.push_back($1); }
// 	| ':' expr_string { $$ = new ExprListAST(':'); $$->exprs.push_back(new ExprListAST('\0')); $$->exprs.push_back($2); }
// 	| expr_idx ':' { ($$ = $1)->exprs.push_back(new ExprListAST('\0')); }
// 	| expr_idx ':' expr_string { ($$ = $1)->exprs.push_back($3); }
// 	| expr_idx ':' ELLIPSIS { ($$ = $1)->exprs.back() = new EllipsisExprAST(getloc(@3, @3), $1->exprs.back()); }
// ;

expr_idx
	: _expr { $$ = new ExprListAST(':'); $$->exprs.push_back($1); }
	| ':' { $$ = new ExprListAST(':'); $$->exprs.push_back(new ExprListAST('\0')); $$->exprs.push_back(new ExprListAST('\0')); }
	| expr_idx ':' { ($$ = $1)->exprs.push_back(new ExprListAST('\0')); }
	| expr_idx _expr { ((ExprListAST*)($$ = $1)->exprs.back())->exprs.push_back($2); }
	| expr_idx ELLIPSIS { ((ExprListAST*)($$ = $1)->exprs.back())->exprs.back() = new EllipsisExprAST(getloc(@2, @2), ((ExprListAST*)$1->exprs.back())->exprs.back()); }
;

id_or_plchld
	: ID { $$ = new IdExprAST(getloc(@1, @1), $1); }
	| PLCHLD1 { $$ = new PlchldExprAST(getloc(@1, @1), $1); }
	| PLCHLD2 { $$ = new PlchldExprAST(getloc(@1, @1), $1); }
;

expr
	: _expr { $$ = $1; }
	| _expr ':' _expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)':', ":", $1, $3); }
;

_expr
	: LITERAL { $$ = new LiteralExprAST(getloc(@1, @1), $1); }
	| PARAM { $$ = new ParamExprAST(getloc(@1, @1), $1); }
	| id_or_plchld { $$ = $1; }

	// Enclosed expressions
	| '(' optional_stmt ')' %prec ENC { $$ = new EncOpExprAST(getloc(@1, @3), (int)'(', "(", ")", $2); }
	| '[' expr_idx ']' %prec ENC { $$ = new EncOpExprAST(getloc(@1, @3), (int)'[', "[", "]", $2); }
	| '{' optional_stmt '}' %prec ENC { $$ = new EncOpExprAST(getloc(@1, @3), (int)'{', "{", "}", $2); }

	// Parameterized expressions
	| expr '(' optional_stmt ')' %prec CALL { $$ = new ArgOpExprAST(getloc(@1, @4), (int)'(', "(", ")", $1, $3); }
	| expr '[' expr_idx ']' %prec SUBSCRIPT { $$ = new ArgOpExprAST(getloc(@1, @4), (int)'[', "[", "]", $1, $3); }

	// Tertiary operators
	| expr FOR expr IN expr { $$ = new TerOpExprAST(getloc(@1, @5), (int)token::FOR, (int)token::IN, "for", "in", $1, $3, $5); }

	// Binary operators
	| expr '=' expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)'=', "=", $1, $3); }
	| expr CADD expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)token::CADD, "+=", $1, $3); }
	| expr CSUB expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)token::CSUB, "-=", $1, $3); }
	| expr CMUL expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)token::CMUL, "*=", $1, $3); }
	| expr CMML expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)token::CMML, "@=", $1, $3); }
	| expr CDIV expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)token::CDIV, "/=", $1, $3); }
	| expr CMOD expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)token::CMOD, "%=", $1, $3); }
	| expr CAND expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)token::CAND, "&=", $1, $3); }
	| expr COR expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)token::COR, "|=", $1, $3); }
	| expr CXOR expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)token::CXOR, "^=", $1, $3); }
	| expr CLS expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)token::CLS, "<<=", $1, $3); }
	| expr CRS expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)token::CRS, ">>=", $1, $3); }
	| expr CPOW expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)token::CPOW, "**=", $1, $3); }
	| expr CIDV expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)token::CIDV, "//=", $1, $3); }
	| expr '.' id_or_plchld { $$ = new BinOpExprAST(getloc(@1, @3), (int)'.', ".", $1, $3); }
	| expr '.' ELLIPSIS { $$ = new VarBinOpExprAST(getloc(@1, @3), (int)'.', ".", $1); }
	| expr DM id_or_plchld { $$ = new BinOpExprAST(getloc(@1, @3), (int)token::DM, "->", $1, $3); }
	| expr '+' expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)'+', "+", $1, $3); }
	| expr '-' expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)'-', "-", $1, $3); }
	| expr '*' expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)'*', "*", $1, $3); }
	| expr '/' expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)'/', "/", $1, $3); }
	| expr IDIV expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)token::IDIV, "//", $1, $3); }
	| expr '%' expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)'%', "%", $1, $3); }
	| expr '&' expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)'&', "&", $1, $3); }
	| expr EQ expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)token::EQ, "==", $1, $3); }
	| expr NE expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)token::NE, "!=", $1, $3); }
	| expr GEQ expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)token::GEQ, ">=", $1, $3); }
	| expr LEQ expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)token::LEQ, "<=", $1, $3); }
	| expr '>' expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)'>', ">", $1, $3); }
	| expr '<' expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)'<', "<", $1, $3); }
	| expr RS expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)token::RS, ">>", $1, $3); }
	| expr LS expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)token::LS, "<<", $1, $3); }
	| expr AND expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)token::AND, "&&", $1, $3); }
	| expr OR expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)token::OR, "||", $1, $3); }
	| expr IN expr { $$ = new BinOpExprAST(getloc(@1, @3), (int)token::IN, "in", $1, $3); }
	| FOR expr IN expr { $$ = new BinOpExprAST(getloc(@1, @4), (int)token::FOR, "forin", $2, $4); }

	// Unary operators
	| '+' expr { $$ = new PrefixExprAST(getloc(@1, @2), (int)'+', "+", $2); } //TODO: Precedence
	| '-' expr { $$ = new PrefixExprAST(getloc(@1, @2), (int)'-', "-", $2); } //TODO: Precedence
	| '*' expr { $$ = new PrefixExprAST(getloc(@1, @2), (int)'*', "*", $2); }
	| expr '*' { $$ = new PostfixExprAST(getloc(@1, @2), (int)'*', "*", $1); }
	| '!' expr { $$ = new PrefixExprAST(getloc(@1, @2), (int)'!', "!", $2); }
	| '&' expr %prec REF { $$ = new PrefixExprAST(getloc(@1, @2), (int)'&', "&", $2); }
	| '@' expr %prec DCOR { $$ = new PrefixExprAST(getloc(@1, @2), (int)'@', "@", $2); }
	| AWT expr { $$ = new PrefixExprAST(getloc(@1, @2), (int)token::AWT, "await", $2); }
	| NEW expr { $$ = new PrefixExprAST(getloc(@1, @2), (int)token::NEW, "new", $2); }

	// List operators
	| expr ',' { $$ = new ExprListAST(',', { $1 }); }
	| expr ',' ELLIPSIS {
		if ($1->exprtype == ExprAST::ExprType::LIST)
			((ExprListAST*)($$ = $1))->exprs.back() = new EllipsisExprAST(getloc(@3, @3), ((ExprListAST*)$1)->exprs.back());
		else
			$$ = new ExprListAST(',', { new EllipsisExprAST(getloc(@3, @3), $1) });
	}
	| expr ',' expr {
		ExprListAST *l1 = (ExprListAST*)$1, *l3 = (ExprListAST*)$3;
		if ($1->exprtype == ExprAST::ExprType::LIST && $3->exprtype == ExprAST::ExprType::LIST)
			l1->exprs.insert(l1->end(), l3->begin(), l3->end());
		else if ($1->exprtype == ExprAST::ExprType::LIST && $3->exprtype != ExprAST::ExprType::LIST)
			l1->exprs.push_back($3);
		else if ($1->exprtype != ExprAST::ExprType::LIST && $3->exprtype == ExprAST::ExprType::LIST)
			(l1 = new ExprListAST(',', { $1 }))->exprs.insert(l1->end(), l3->begin(), l3->end());
		else
			l1 = new ExprListAST(',', { $1, $3 });
		$$ = l1;
	}
;

%%

void yy::PyParser::error( const location_type &l, const std::string &err_message )
{
	std::cerr << "Error: " << err_message << " at " << l << "\n"; //TODO: throw syntax error
}

BlockExprAST* parsePythonFile(const char* filename)
{
	// Open source file
	std::ifstream in(filename);
	if (!in.good())
	{
		std::cerr << "\033[31merror:\033[0m " << std::string(filename) << ": No such file or directory\n";
		return nullptr;
	}

	// Parse file into rootBlock
	BlockExprAST* rootBlock;
	PyLexer lexer(&in, &std::cout);
	yy::PyParser parser(lexer, filename, &rootBlock);
	parser.parse();

	// Close source file
	in.close();

	return rootBlock;
}

const std::vector<ExprAST*> parsePythonTplt(const char* tpltStr)
{
	// Parse tpltStr into tpltBlock
	std::stringstream ss(tpltStr);
	PyLexer lexer(ss, std::cout);
	BlockExprAST* tpltBlock;
	yy::PyParser parser(lexer, nullptr, &tpltBlock);
	if (parser.parse())
	{
		std::cerr << "\033[31merror:\033[0merror parsing template " << std::string(tpltStr) << '\n';
		return {};
	} //TODO: Throw CompileError instead:
		//throw CompileError("error parsing template " + std::string(tpltStr));

	// Remove trailing STOP expr
	if (tpltBlock->exprs->size())
		tpltBlock->exprs->pop_back();

	return *tpltBlock->exprs;
}